################################
# Imports
################################
import os
import shutil
from seqnado import Assay
from seqnado.config import SeqnadoConfig
from seqnado.inputs import (
    SampleGroupings,
    SampleGroups,
    select_sample_collection,
)
from seqnado.outputs import SeqnadoOutputFactory, SeqnadoOutputFiles
from seqnado.helpers import remove_unwanted_run_files


################################
# Hardcoded Config
################################
SCALE_RESOURCES = float(os.environ.get("SCALE_RESOURCES", "1"))


container: "oras://ghcr.io/alsmith151/seqnado_pipeline:latest"


################################
# Load Configuration
################################
CONFIG = SeqnadoConfig(**config)  # pyright: ignore[reportUndefinedVariable]
ASSAY = CONFIG.assay
INPUT_FILES = select_sample_collection(ASSAY).from_csv(CONFIG.metadata)


################################
# Define Sample Groupings
################################
SAMPLE_GROUPINGS = SampleGroupings()

normalisation_groups = SampleGroups.from_dataframe(
    INPUT_FILES.to_dataframe(),
    subset_column="norm_group",
    include_controls=True,
)

scaling_groups = SampleGroups.from_dataframe(
    INPUT_FILES.to_dataframe(),
    subset_column="scaling_group",
    include_controls=True,
)

consensus_groups = SampleGroups.from_dataframe(
    INPUT_FILES.to_dataframe(),
    subset_column="consensus_group",
    include_controls=True,
)

SAMPLE_GROUPINGS.add_grouping("normalisation", normalisation_groups)
SAMPLE_GROUPINGS.add_grouping("scaling", scaling_groups)
SAMPLE_GROUPINGS.add_grouping("consensus", consensus_groups)


################################
# Define Outputs
################################
OUTPUTS: SeqnadoOutputFiles = (
    SeqnadoOutputFactory(
        assay=ASSAY,
        samples=INPUT_FILES,
        config=CONFIG,
        sample_groupings=SAMPLE_GROUPINGS,
    )
    .create_output_builder()
    .build()
)

SAMPLE_NAMES = OUTPUTS.sample_names


################################
# Set-Up Workflow
################################
INPUT_FILES.symlink_fastq_files(output_dir="seqnado_output/fastqs/")


################################
# Include Rules
################################
include: "rules/common/setup.smk"
include: "rules/qc.smk"

if ASSAY == Assay.RNA:
    include: "rules/alignment/rna.smk"
    include: "rules/bam/index.smk"
    include: "rules/bam/blacklist.smk"
    include: "rules/bam/duplicates.smk"
    include: "rules/bam/manipulate.smk"
    include: "rules/bam/filter.smk"
    include: "rules/quant/featurecounts.smk"
    include: "rules/quant/salmon.smk"
    include: "rules/deseq2_rna.smk"
    include: "rules/fastq/screen.smk"
    include: "rules/fastq/trim.smk"
    include: "rules/heatmap.smk"
    include: "rules/hub.smk"
    include: "rules/pileup/normalization.smk"
    include: "rules/pileup.smk"
    include: "rules/geo_submission.smk"
    include: "rules/visualisation.smk"
elif ASSAY == Assay.CAT or ASSAY == Assay.CHIP:
    include: "rules/alignment/dna.smk"
    include: "rules/bam/index.smk"
    include: "rules/bam/blacklist.smk"
    include: "rules/bam/duplicates.smk"
    include: "rules/bam/manipulate.smk"
    include: "rules/bam/filter.smk"
    include: "rules/fastq/screen.smk"
    include: "rules/fastq/trim.smk"
    include: "rules/normalize.smk"
    include: "rules/peak_call.smk"
    include: "rules/pileup.smk"
    include: "rules/pileup/normalization.smk"
    include: "rules/heatmap.smk"
    include: "rules/hub.smk"
    include: "rules/geo_submission.smk"
    include: "rules/visualisation.smk"
    include: "rules/consensus.smk"
    include: "rules/dataset.smk"
elif ASSAY == Assay.CRISPR:
    include: "rules/fastq/screen.smk"
    include: "rules/crispr.smk"
    include: "rules/bam/index.smk"
    include: "rules/bam/blacklist.smk"
    include: "rules/bam/duplicates.smk"
    include: "rules/bam/manipulate.smk"
    include: "rules/bam/filter.smk"
elif ASSAY == Assay.MCC:
    include: "rules/fastq/trim.smk"
    include: "rules/fastq/manipulate.smk"
    include: "rules/alignment/dna.smk"
    include: "rules/bam/index.smk"
    include: "rules/bam/blacklist.smk"
    include: "rules/bam/duplicates.smk"
    include: "rules/bam/manipulate.smk"
    include: "rules/bam/filter.smk"
    include: "rules/pileup.smk"
    include: "rules/hub.smk"
    include: "rules/mcc.smk"
elif ASSAY == Assay.METH:
    include: "rules/alignment/dna.smk"
    include: "rules/bam/index.smk"
    include: "rules/bam/blacklist.smk"
    include: "rules/bam/duplicates.smk"
    include: "rules/bam/manipulate.smk"
    include: "rules/bam/filter.smk"
    include: "rules/fastq/screen.smk"
    include: "rules/fastq/trim.smk"
    include: "rules/geo_submission.smk"
    include: "rules/meth_calling.smk"
elif ASSAY == Assay.SNP:
    include: "rules/alignment/dna.smk"
    include: "rules/bam/index.smk"
    include: "rules/bam/blacklist.smk"
    include: "rules/bam/duplicates.smk"
    include: "rules/bam/manipulate.smk"
    include: "rules/bam/filter.smk"
    include: "rules/fastq/screen.smk"
    include: "rules/fastq/trim.smk"
    include: "rules/variant.smk"
    include: "rules/geo_submission.smk"



################################
# Rule Ordering
################################
if CONFIG.has_spikein:
    ruleorder: move_ref_bam > align_paired > align_single
    ruleorder: deeptools_make_bigwigs_spikein > deeptools_make_bigwigs
    ruleorder: align_paired_spikein > align_paired > align_single_spikein > align_single

else:
    ruleorder: align_paired > align_single > move_ref_bam
    ruleorder: align_paired > align_paired_spikein > align_single > align_single_spikein 


################################
# Final Aggregation Rule
################################
rule all:
    input:
        OUTPUTS.all_files,


################################
# Workflow Hooks
################################
onsuccess:
    remove_unwanted_run_files()


onerror:
    log_out = "seqnado_error.log"
    shutil.copyfile(log, log_out)  # pyright: ignore[reportUndefinedVariable]
    print(
        f"An error occurred. Please check the log file {log_out} for more information."
    )
    remove_unwanted_run_files()
