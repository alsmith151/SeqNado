import os
import sys
import shutil
from datetime import datetime
import glob
from snakemake.utils import min_version
import seqnado.utils as utils
import pandas as pd

ASSAY = "ATAC"
configfile: "config_atac.yml"
# container: "library://asmith151/seqnado/seqnado_pipeline:latest"
container: "/ceph/project/milne_group/cchahrou/reference/seqnado/singularity/d339775fb621852b9951e65bbed1c08f.simg"

utils.format_config_dict(config)
PILEUP_METHODS = config["pileup_method"]
PEAK_CALL_METHODS = config["peak_calling_method"]

# Get experiment design
if os.path.exists(config["design"]):
    # Expect columns - sample fq1 fq2
    FASTQ_SAMPLES = utils.GenericFastqSamples(
        pd.read_csv(config["design"], sep="[\s+,\t]", engine="python")
    )
    assert FASTQ_SAMPLES.design.shape[0] > 0, "No samples found in design file"
    for col in ["sample", "fq1", "fq2"]:
        assert col in FASTQ_SAMPLES.design.columns, f"Design file must contain columns sample, fq1, fq2. Columns found: {FASTQ_SAMPLES.design.columns}"

else:
    # Use pattern matching to get samples
    fq_files = list(utils.get_fastq_files("."))
    if fq_files:
        FASTQ_SAMPLES = utils.GenericFastqSamples.from_files(fq_files)
    else:
        raise ValueError("No FASTQ files found in the working directory")



DESIGN = FASTQ_SAMPLES.design
SAMPLE_NAMES = FASTQ_SAMPLES.sample_names_all


include: "rules/qc.smk"
include: "rules/fastq_trim.smk"
include: "rules/align.smk"
include: "rules/alignment_post_processing.smk"
include: "rules/peak_call_atac.smk"
include: "rules/pileup.smk"
include: "rules/heatmap_atac.smk"
include: "rules/hub.smk"
# Define output files
ANALYSIS_OUTPUT = [
    "seqnado_output/qc/full_qc_report.html"
]

if config["make_bigwigs"] == "yes":
    ANALYSIS_OUTPUT.append(
        expand(
            "seqnado_output/bigwigs/{method}/{sample}.bigWig",
            sample=SAMPLE_NAMES,
            method=PILEUP_METHODS,
        ),
    )
if config["call_peaks"] == "yes":
    ANALYSIS_OUTPUT.append(
        expand(
            "seqnado_output/peaks/{method}/{sample}.bed",
            sample=SAMPLE_NAMES,
            method=PEAK_CALL_METHODS,
        ),
    )
if config["make_heatmaps"] == "yes":
    ANALYSIS_OUTPUT.append(
        expand(
            "seqnado_output/heatmap/{method}/{sample}.png",
            sample=SAMPLE_NAMES,
            method=PILEUP_METHODS,
        ),
    )
if config["make_ucsc_hub"] == "yes":
    ANALYSIS_OUTPUT.append(
        os.path.join(
            config["ucsc_hub_details"]["directory"],
            f"{config['ucsc_hub_details']['name']}.hub.txt",
        ),
    )


rule all:
    input:
        ANALYSIS_OUTPUT

onsuccess:
    slurm_files = glob.glob("slurm-*.out")
    sps_files = glob.glob("sps-*")

    for fn in [*slurm_files, *sps_files]:
        try:
            if not os.path.isdir(fn):
                os.remove(fn)
            else:
                shutil.rmtree(fn)

        except Exception as e:
            print(e)

onerror:
    log_out = "seqnado_error.log"
    shutil.copyfile(log, log_out)
    print(
        f"An error occurred. Please check the log file {log_out} for more information."
    )
