import os
import sys
import shutil
from datetime import datetime
import glob
from snakemake.utils import min_version
import ngs_pipeline.utils as utils
import pandas as pd

start_time = datetime.now()
ASSAY = "ATAC"


configfile: "config_atac.yml"


container: "library://asmith151/seqnado/ngs:latest"


utils.format_config_dict(config)
pipeline_tools = utils.get_pipeline_tools(config, assay=ASSAY)

# Methods to use
PILEUP_METHODS = [
    method for method in ["deeptools", "homer"] if pipeline_tools[method] == True
]
PEAK_CALL_METHODS = [
    method
    for method in ["macs", "lanceotron", "homer"]
    if pipeline_tools[method] == True
]
DUPLICATE_REMOVAL_METHODS = {
    "picard": "rules/remove_pcr_duplicates_picard.smk",
    "deeptools": "rules/remove_pcr_duplicates_deeptools.smk",
}
DUPLICATE_REMOVAL_RULE = DUPLICATE_REMOVAL_METHODS.get(
    config["remove_pcr_duplicates_method"], "rules/remove_pcr_duplicates_ignore.smk"
)


# Get dataframe of samples
if os.path.exists(config["design"]):
    # Expect columns - sample fq1 fq2
    ATAC_SAMPLES = utils.GenericFastqSamples(
        pd.read_csv(config["design"], sep="[\s+,\t]", engine="python")
    )

else:
    # Use pattern matching to get samples
    fq_files = list(utils.get_fastq_files("."))
    if fq_files:
        ATAC_SAMPLES = utils.GenericFastqSamples.from_files(fq_files)
    else:
        raise ValueError("No FASTQ files found in the working directory")


ATAC_SAMPLES.symlink_fastq_files(outdir="fastq")


DESIGN = ATAC_SAMPLES.design
FASTQ_FILES = ATAC_SAMPLES.fastq_files
SAMPLE_NAMES = ATAC_SAMPLES.sample_names_all


include: DUPLICATE_REMOVAL_RULE
include: "rules/qc.smk"
include: "rules/read_trimming.smk"
include: "rules/align.smk"
include: "rules/alignment_post_processing.smk"
include: "rules/peak_call.smk"
include: "rules/pileup.smk"
include: "rules/hub.smk"


rule all:
    input:
        mutiqc_full="qc/full_qc_report.html",
        pileups=expand(
            "bigwigs/{method}/{sample}.bigWig",
            sample=SAMPLE_NAMES,
            method=PILEUP_METHODS,
        ),
        peaks=expand(
            "peaks/{method}/{sample}.bed",
            sample=SAMPLE_NAMES,
            method=PEAK_CALL_METHODS,
        ),
        hub=os.path.join(
            config["ucsc_hub_details"]["directory"],
            f"{config['ucsc_hub_details']['name']}.hub.txt",
        ),


onsuccess:
    slurm_files = glob.glob("slurm-*.out")
    sps_files = glob.glob("sps-*")

    for fn in [*slurm_files, *sps_files]:
        try:
            if not os.path.isdir(fn):
                os.remove(fn)
            else:
                shutil.rmtree(fn)

        except Exception as e:
            print(e)

onerror:
    log_out = "ngs_pipeline_error.log"
    shutil.copyfile(log, log_out)
    print(
        f"An error occurred. Please check the log file {log_out} for more information."
    )
